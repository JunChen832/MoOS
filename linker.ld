/* set the entry point */
ENTRY(loader)
/* name the format to use for the output file */
OUTPUT_FORMAT(elf32-i386)
/* specify a paricular output machine architecture */
OUTPUT_ARCH(i386:i386)

/* the SECTIONS command tells the linker how to map input sections into output sections, and how to place the output sections in memory */
SECTIONS
{
  . = 0x0100000;

  .text :
  {
    *(.multiboot)
    *(.text*)
    /* read only data */
    *(.rodata)
  }

  .data  :
  {
    start_ctors = .;
    /* .init_array
     * global object generated by constructor in .init_array
     * use KEEP to forbid releasing the resources
     */
    KEEP(*( .init_array ));
    /* SORT_BY_INIT_PRIORITY will sort sections into ascending order by 
     * numerical value of the GCC init_priority attribute encoded in the 
     * section name before placing them in the output file 
     */
    KEEP(*(SORT_BY_INIT_PRIORITY( .init_array.* )));
    end_ctors = .;

    *(.data)
  }

  .bss  :
  {
    *(.bss)
  }

  /DISCARD/ : { *(.fini_array*) *(.comment) }
}
